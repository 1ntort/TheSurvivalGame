brig command /thesurvivalgame:tsg-hidden-yesno: # @skignore
    description: A command for TheSurvivalGame's YesNo effect. This command does nothing on its own. (HIDDEN)
    permission: thesurvivalgame.hidden.yesno
    arguments:
        register literal arg named "type" using "no" and "yes"
    trigger:
        if {_type} is "no":
            send "&8&m   &b You selected option ""&cNo&b""."
        else if {_type} is "yes":
            send "&8&m   &b You selected option ""&aYes&b""."

function TheSurvivalGame_buildMessage_yesNo(player: player, message: string, yes command: string = "none", no command: string = "none") :: string:
    set {_yes component} to text component from "&aYes"
    set hover event of {_yes component} to a new hover event showing "&bClick to select ""&aYes&b""."
    if {_yes command} is "none":
        set click event of {_yes component} to a new click event to run command "/thesurvivalgame:tsg-hidden-yesno yes"
    else:
        set click event of {_yes component} to a new click event to run command "%{_yes command}%"
    
    set {_no component} to text component from "&cNo"
    set hover event of {_no component} to a new hover event showing "&bClick to select ""&cNo&b""."
    if {_no command} is "none":
        set click event of {_no component} to a new click event to run command "/thesurvivalgame:tsg-hidden-yesno no"
    else:
        set click event of {_no component} to a new click event to run command "%{_no command}%"
    
    send "&f" to {_player}
    send "&8 ◆&m                                                                      &8◆ " to {_player}
    send "&f" to {_player}
    send "&8 |&a %{_message}%" to {_player}
    send "&f" to {_player}
    send component (merge components "&8 &m            &8 [&b1&8] " and {_yes component}) to {_player}
    send component (merge components "&8 &m            &8 [&b2&8] " and {_no component}) to {_player}
    send "&f" to {_player}
    send "&8 ◆&m                                                                      &8◆ " to {_player}
    send "&f" to {_player}